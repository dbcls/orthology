#!/usr/bin/env spang2

# @title Get orthologs of a specific protein
# @endpoint http://sparql.nibb.ac.jp/sparql
# @param uniprot=Q96C23

PREFIX sio: <http://semanticscience.org/resource/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX oo: <http://purl.org/net/orth#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX uniprot: <http://purl.uniprot.org/uniprot/>

SELECT DISTINCT ?up ?idtax
WHERE {
  ?query sio:SIO_010078 uniprot:{{uniprot}} .
  ?group oo:hasHomologous ?node1 .
  ?node1 oo:hasHomologous* ?query .
  ?group oo:hasHomologous ?node2 .
  ?node2 oo:hasHomologous* ?target .
  ?target a oo:Gene .
  ?target obo:RO_0002162 ?idtax .
  ?target sio:SIO_010078 ?up .
  FILTER (REGEX (STR (?up), 'uniprot'))
  FILTER(?node1 != ?node2)
}

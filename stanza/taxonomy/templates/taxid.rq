#!/usr/bin/env spang2

# @title get children of a taxon
# @endpoint test
# @param taxid=40674

PREFIX taxon: <http://ddbj.nig.ac.jp/ontologies/taxonomy/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX taxid: <http://identifiers.org/taxonomy/>

SELECT ?order ?order_name ?family ?family_name ?species ?species_name
WHERE {
  ?species taxon:rank taxon:Species ;
      rdfs:label ?species_name ;
      rdfs:subClassOf+ taxid:40674 ;
      rdfs:subClassOf+ ?family ;
      rdfs:subClassOf+ ?order .
  ?family taxon:rank taxon:Family ;
      rdfs:label ?family_name .
  ?order taxon:rank taxon:Order ;
      rdfs:label ?order_name .
}
ORDER BY ?order_name ?family_name ?species_name

